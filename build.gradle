import org.apache.tools.ant.filters.*

buildscript
{
	repositories
  	{ 
  		mavenCentral()
	  	maven
	    {
	        name = "forge"
	        url = "http://files.minecraftforge.net/maven"
	    }
	    maven
	    {
	        name = "sonatype"
	        url = "https://oss.sonatype.org/content/repositories/snapshots/"
	    }
  	}
  	dependencies
  	{
      classpath 'net.minecraftforge.gradle:ForgeGradle:1.2-SNAPSHOT'
  	}
}

repositories
{
    // Spigot
    maven
    {
    	url "https://hub.spigotmc.org/nexus/content/groups/public/"
    }
    // ProtocolLib
    maven
    {
    	url "http://repo.dmulloy2.net/content/groups/public/"
    }
    // Vault
    maven
    {
    	url "http://nexus.theyeticave.net/content/repositories/pub_releases/"
    }
    // jOhm
    maven
    {
    	url "https://raw.githubusercontent.com/Dasfaust/johm/mvn-repo/"
    }
}

apply plugin: 'java'
apply plugin: 'forge'

sourceCompatibility = 1.7
targetCompatibility = 1.7

ext.env = System.getenv()
ext.build = env.BUILD_NUMBER
if (ext.build == null)
{
    build = "dev"
}
version = "2.0.0." + build
group = "me.dasfaust.gm"

minecraft
{
    version = "1.7.10-10.13.2.1236"
    srgExtra "PK: redis/clients/jedis lib/jedis/jedis"
    srgExtra "PK: redis/clients/util lib/jedis/util"
}

configurations
{
    shade
    compile.extendsFrom shade
}

dependencies
{
    compile group: 'org.bukkit', name: 'bukkit', version: '1.9-R0.1-SNAPSHOT'
    compile group: 'net.milkbowl.vault', name: 'Vault', version: '1.5.0'
    compile group: 'com.comphenix.protocol', name: 'ProtocolLib', version: '3.6.4'
    shade group: 'org.apache.commons', name: 'commons-io', version: '1.3.2'
    shade group: 'redis.clients', name: 'jedis', version: '2.7.2'
    shade group: 'redis', name: 'johm', version: '0.6.3'
    shade group: 'org.apache.commons', name: 'commons-pool2', version: '2.4.1'
    shade group: 'commons-codec', name: 'commons-codec', version: '1.10'
    // Packaging this because CraftBukkit's package changed from 1.7 -> 1.8
    shade group: 'com.google.code.gson', name: 'gson', version: '2.3.1'
    compile group: 'org.hsqldb', name: 'hsqldb', version: '2.3.2'
}

jar
{
    configurations.shade.each { dep ->
        from(project.zipTree(dep))
        {
            exclude 'META-INF', 'META-INF/**', 'junit/**', '**.txt', 'org/junit/**', 'org/hamcrest/**'
        }
    }
}

processResources
{
    filter ReplaceTokens, tokens: [
            "version": project.version
    ]
}
